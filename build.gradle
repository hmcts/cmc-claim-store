import java.util.stream.Collectors

plugins {
  id 'application'
  id 'uk.gov.hmcts.java' version '0.12.43'
  id 'checkstyle'
  id 'io.spring.dependency-management' version '1.1.0'
  id 'org.springframework.boot' version '2.7.18'
  id 'org.owasp.dependencycheck' version '8.0.1'
  id 'org.sonarqube' version '5.0.0.4638'
  id 'com.github.ben-manes.versions' version '0.51.0'
  id 'jacoco'
  id 'idea'
  id 'au.com.dius.pact' version '4.6.5'
}

dependencyUpdates.resolutionStrategy = {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

def springBootVersion = plugins.getPlugin('org.springframework.boot').class.package.implementationVersion
def springCloudVersion = '2021.0.9'

def versions = [
  logback           : '1.2.13',
  springBoot        : springBootVersion,
  restAssuredVersion: '4.2.0',
  jackson           : '2.17.0',
  junit             : '5.10.1',
  junitPlatform     : '1.10.2',
  pact_version    : '4.1.7',
  serviceAuthVersion : '4.0.3',
  idamJavaClient : '2.0.1',
  elasticSearch     : '7.17.20'
]

static def getCheckedOutGitCommitHash() {
  'git rev-parse --verify --short HEAD'.execute().text.trim()
}

allprojects {
  apply plugin: 'java'
  apply plugin: 'checkstyle'
  apply plugin: 'io.spring.dependency-management'
  apply plugin: 'org.owasp.dependencycheck'
  apply plugin: 'com.github.ben-manes.versions'

  group 'uk.gov.hmcts.cmc'
  version '1.1.0'

  java {
    toolchain {
      languageVersion = JavaLanguageVersion.of(17)
    }
  }

  // https://github.com/gradle/gradle/issues/16791
  tasks.withType(JavaExec).configureEach {
    javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
  }

  project.ext {
    pactVersion = getCheckedOutGitCommitHash()
  }

  jacoco {
    toolVersion = '0.8.8' // jacocoMavenPluginVersion
    reportsDirectory = file("$buildDir/reports/jacoco")
  }

  tasks.withType(Copy).all {
    duplicatesStrategy 'include'
  }

  checkstyle {
    maxWarnings = 0
    toolVersion = '8.29'
    getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
  }

  // https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
  dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'false' ? 11 : 0
    suppressionFile = 'config/owasp/suppressions.xml'
    analyzers {
      // Disable scanning of .NET related binaries
      assemblyEnabled = false
    }
  }

  sourceSets {
    integrationTest {
      java {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        srcDir('src/integrationTest/java')
      }
      resources {
        srcDir('src/integrationTest/resources')
      }
    }

    aat {
      java {
        compileClasspath += main.output
        runtimeClasspath += main.output
        srcDir('src/aat/java')
      }
      resources {
        srcDir('src/aat/resources')
      }
    }

    contractTest {
      java {
        compileClasspath += main.output
        runtimeClasspath += main.output
        srcDir file('src/contractTest/java')
      }
      resources.srcDir file('src/contractTest/resources')
    }
  }

  compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
  }

  compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
  }

  repositories {
    mavenLocal()
    mavenCentral()
    maven {
      url 'https://jitpack.io'
    }
    jcenter()
  }

  dependencyManagement {
    imports {
      mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
      mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
    dependencies {
      // checkstyle needs a certain version and so does spring fox swagger, so can't use latest
      // fun times...
      dependency group: 'com.google.guava', name: 'guava', version: '33.1.0-jre'

      dependency group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson
      dependency group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: versions.jackson
      dependency group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: versions.jackson

      dependency group: 'org.bouncycastle', name: 'bcprov-jdk18on', version: '1.77'

      dependencySet(
        group: 'org.yaml',
        version: '2.0'
      ) {
        entry 'snakeyaml'
      }

      dependencySet(group: 'ch.qos.logback', version: '1.2.13') {
        entry 'logback-core'
        entry 'logback-classic'
      }

      dependencySet(
        group: 'org.hibernate',
        version: '8.0.1.Final'
      ) {
        entry 'hibernate-validator'
      }
    }
  }
}

configurations {
  aatImplementation.extendsFrom(testImplementation)
  aatRuntime.extendsFrom(testRuntime)
  integrationTestImplementation.extendsFrom(testCompile)
  integrationTestRuntime.extendsFrom(testRuntime)
}

configurations.all {
  exclude group: 'com.vaadin.external.google', module: 'android-json'
}

dependencies {

  implementation project(':email-client')
  implementation project(':domain-model')
  implementation project(':ccd-adapter')
  implementation project(':ccd-sample-data')
  implementation project(':rpa-mapper')
  implementation project(':domain-sample-data')
  implementation project(':job-scheduler')
  implementation project(':launch-darkly-client')

  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-quartz'

  implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.20.0'
  implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.20.0'

  implementation group: 'org.flywaydb', name: 'flyway-core', version: '5.2.4'


  implementation group: 'ch.qos.logback', name: 'logback-classic', version: versions.logback
  implementation group: 'ch.qos.logback', name: 'logback-core', version: versions.logback

  implementation group: 'com.mitchellbosecke', name: 'pebble', version: '2.4.0'

  implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'

  implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.6.15'

  implementation group: 'org.elasticsearch', name: 'elasticsearch', version: versions.elasticSearch
  implementation group: 'org.postgresql', name: 'postgresql', version: '42.7.2'

  implementation group: 'uk.gov.service.notify', name: 'notifications-java-client', version: '5.0.1-RELEASE'

  implementation group: 'uk.gov.hmcts.reform', name: 'logging', version: '5.1.7'
  implementation group: 'uk.gov.hmcts.reform', name: 'logging-appinsights', version: '5.1.7'
  implementation group: 'com.microsoft.azure', name: 'applicationinsights-web', version: '2.6.4'

  implementation group: 'com.github.hmcts', name: 'java-logging', version: '6.1.4'

  implementation('com.github.hmcts:cmc-pdf-service-client:7.0.0') {
    exclude group: 'java-logging', module: ' java-logging'
  }
  implementation group: 'uk.gov.hmcts.reform', name: 'document-management-client', version: '7.0.0'
  implementation group: 'com.launchdarkly', name: 'launchdarkly-java-server-sdk', version: '6.3.0'
  implementation group: 'uk.gov.hmcts.reform', name: 'core-case-data-store-client', version: '4.7.6'
  implementation group: 'com.github.hmcts', name: 'ccd-case-document-am-client', version: '1.7.1'
  implementation group: 'com.github.hmcts', name: 'doc-assembly-client', version: '1.2.2'
  implementation('com.github.hmcts:payments-java-client:0.0.7') {
    exclude group: 'java-logging', module: ' java-logging'
  }
  implementation group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '4.0.0'
  implementation group: 'com.github.hmcts', name: 'send-letter-client', version: '3.0.16'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation', version: versions.springBoot

  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-cache'
  implementation group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '3.1.8'

  implementation group: 'org.springframework.security', name: 'spring-security-web'
  implementation group: 'org.springframework.security', name: 'spring-security-config'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-oauth2-client'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-oauth2-resource-server'
  implementation group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '9.37.3'
  implementation group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.1'

  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.14.0'
  implementation group: 'org.apache.commons', name: 'commons-csv', version: '1.8'

  implementation group: 'org.jdbi', name: 'jdbi', version: '2.78'

  implementation group: 'org.springframework', name: 'spring-context-support'
  implementation group: 'org.springframework.retry', name: 'spring-retry'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign'

  implementation group: 'io.github.openfeign', name: 'feign-httpclient', version: '13.2.1'

  implementation group: 'org.springframework.cloud',name : 'spring-cloud-starter-bootstrap'

  implementation group: 'org.json', name: 'json', version: '20240303'

  implementation group: 'javax.json', name: 'javax.json-api', version: '1.1.4'
  implementation group: 'javax.inject', name: 'javax.inject', version: '1'

  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.26'
  compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.26'

  testImplementation project(':domain-sample-data')
  testImplementation project(path: ":ccd-adapter", configuration: 'testArtifacts')
  testImplementation group: 'io.rest-assured', name: 'rest-assured', version: versions.restAssuredVersion
  testImplementation group: 'io.rest-assured', name: 'json-path', version: versions.restAssuredVersion
  testImplementation group: 'io.rest-assured', name: 'rest-assured-common', version: versions.restAssuredVersion
  testImplementation group: 'io.rest-assured', name: 'xml-path', version: versions.restAssuredVersion

  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
  testImplementation group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock'

  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
  testImplementation group: 'org.junit.vintage', name: 'junit-vintage-engine', version: versions.junit
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junit
  testImplementation group: 'org.junit.platform', name: 'junit-platform-commons', version: versions.junitPlatform
  testImplementation group: 'org.junit.platform', name: 'junit-platform-engine', version: versions.junitPlatform
  testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.25.3'
  testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.11.0'
  testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.8.0'
  testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: '2.0.2'
  testImplementation group: 'org.testcontainers', name: 'postgresql', version: '1.18.0'

  testImplementation group: 'org.pdfbox', name: 'com.springsource.org.pdfbox', version: '0.7.3'
  testImplementation group: 'com.jayway.jsonpath', name: 'json-path-assert', version: '2.5.0'
  testImplementation group: 'net.jodah', name: 'failsafe', version: '2.4.0'
  testImplementation group: 'org.xmlunit', name: 'xmlunit-core', version: '2.8.2'
  testImplementation group: 'org.xmlunit', name: 'xmlunit-matchers', version: '2.8.2'
  testImplementation 'com.github.hmcts:fortify-client:1.2.1:all'

  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath

  contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'junit5', version: versions.pact_version
  contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'java8', version: versions.pact_version
  contractTestRuntimeOnly group: 'au.com.dius.pact.consumer', name: 'junit5', version: versions.pact_version
  contractTestRuntimeOnly group: 'au.com.dius.pact.consumer', name: 'java8', version: versions.pact_version
  contractTestImplementation group: 'org.modelmapper', name: 'modelmapper', version: '2.3.7'

  contractTestImplementation group: 'au.com.dius.pact.provider', name: 'junit5', version: versions.pact_version
  contractTestImplementation group: 'au.com.dius.pact.provider', name: 'spring', version: versions.pact_version
  contractTestImplementation group: 'au.com.dius.pact.provider', name: 'junit5spring', version: versions.pact_version
  contractTestImplementation("org.junit.jupiter:junit-jupiter-api:5.7.0")
  contractTestRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.7.0")
  contractTestImplementation('org.junit.jupiter:junit-jupiter-api:5.7.0')
  contractTestRuntimeOnly "org.junit.platform:junit-platform-commons:1.7.0"
  contractTestImplementation group: 'com.github.hmcts', name: 'idam-java-client', version: versions.idamJavaClient
  contractTestImplementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: versions.serviceAuthVersion
  contractTestImplementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  contractTestImplementation sourceSets.main.runtimeClasspath
  contractTestImplementation sourceSets.test.runtimeClasspath
}
configurations.all {
  exclude group: 'com.vaadin.external.google', module: 'android-json'
}

dependencies {
  testImplementation project(path: ':domain-model')
  implementation 'org.projectlombok:lombok:1.18.20'
  integrationTestImplementation(sourceSets.test.output)
  integrationTestImplementation(sourceSets.main.output)
  integrationTestImplementation project(':email-client')
  integrationTestImplementation project(':domain-model')
}



task contract(type: Test) {
  description = 'Runs the consumer Pact tests'
  useJUnitPlatform()
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
  include "uk/gov/hmcts/cmc/claimstore/api/provider/**"

}

task runProviderPactVerification(type: Test) {
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
  systemProperty 'pact.verifier.publishResults', System.getProperty('pact.verifier.publishResults')
  systemProperty 'pact.provider.version', project.pactVersion
}

runProviderPactVerification.finalizedBy pactVerify

pact {
  publish {
    pactDirectory = 'pacts'
    pactBrokerUrl = System.getenv("PACT_BROKER_URL") ?: 'http://localhost:80'
    tags = [System.getenv("PACT_BRANCH_NAME") ?:'Dev']
    version = project.pactVersion
  }
}

task fortifyScan(type: JavaExec)  {
  mainClass.set("uk.gov.hmcts.fortifyclient.FortifyClientMainApp")
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

task apiTest(type: Test) {
  group = 'verification'
  description = 'Runs the api tests'
  setTestClassesDirs(sourceSets.integrationTest.output.classesDirs)
  setClasspath(sourceSets.integrationTest.runtimeClasspath)
}

task migrateClaims() {
  description 'Migrating claims from claim-store database to CCD'

  doLast {
    javaexec {
      main = "-jar"
      args = [
        "ccd-claim-migration/build/libs/ccd-claim-migration-1.1.0.jar"
      ]
    }

    print 'Done'
  }
}

tasks.withType(Test) {
  useJUnitPlatform()
}

task functional(type: Test) {
  group = 'Delivery pipeline'
  description = 'Executes functional tests against an AAT Claim Store instance'
  setTestClassesDirs(sourceSets.aat.output.classesDirs)
  setClasspath(sourceSets.aat.runtimeClasspath)
  include "uk/gov/hmcts/cmc/claimstore/tests/functional/**"
  testLogging.showStandardStreams = true


  environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
}

task smoke(type: Test) {
  group = 'Delivery pipeline'
  description = 'Executes non-destructive smoke tests against a running Claim Store instance'
  setTestClassesDirs(sourceSets.aat.output.classesDirs)
  setClasspath(sourceSets.aat.runtimeClasspath)
  testLogging.showStandardStreams = true
  include "uk/gov/hmcts/cmc/claimstore/tests/smoke/**"

  environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
}

compileIntegrationTestJava {
  options.compilerArgs << '-Xlint:deprecation'
}

jacocoTestReport {
  executionData(test, apiTest)
  reports {
    xml.required = true
    csv.required = false
    xml.outputLocation = file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport
check.dependsOn apiTest
checkstyleMain.shouldRunAfter(compileJava)
test.shouldRunAfter(checkstyleMain, checkstyleAat, checkstyleTest, checkstyleIntegrationTest)
apiTest.shouldRunAfter(test, checkstyleMain, checkstyleAat, checkstyleTest, checkstyleIntegrationTest)

def listFiles(String pattern) {
  return new FileNameFinder()
    .getFileNames("${project.rootDir}", pattern)
    .stream()
    .collect(Collectors.joining(","))
}

sonarqube {
  properties {
    property "sonar.projectName", "CMC :: Claim Store"
    property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    property "sonar.exclusions", [
        "**/ccd/domain/**",
        "**/cmc/domain/models/**",
        "**/cmc/domain/amount/**",
        "**/cmc/ccd/sample/data/**",
        "**/cmc/ccd/mapper/**",
        "**/cmc/claimstore/services/courtfinder/**",
        "**/cmc/claimstore/idam/models/**",
        "**/cmc/claimstore/idam/IdamApi.java",
        "**/ExceptionForClient.java",
        "**/config/**",
        "**/*Exception.java",
        "**/*Event.java",
        "**/PathPatterns.java",
        "**/ClaimRepository.java",
        "**/CCDCaseRepository.java",
        "**/cmc/claimstore/appinsights/**",
        "**/cmc/claimstore/constants/**",
        "**/cmc/claimstore/models/**",
        "**/cmc/claimstore/services/ccd/legaladvisor/**",
        "**/cmc/claimstore/services/ccd/callbacks/ioc/FeesAndPaymentsConfiguration.java",
        "**/cmc/claimstore/services/ccd/callbacks/HWFMiscellaneousCallbackHandler.java",
        "**/cmc/claimstore/services/ccd/callbacks/HWFFullAndPartRemissionCallbackHandler.java",
        "**/cmc/claimstore/courtfinder/models/**",
        "**/ccd/migration/**",
        "**/EmailData.java",
        "**/CCDCaseHandler.java",
        "**/CCDReDetermination.java",
        "**/CCDEventProducer.java",
        "**/GeneratedDocuments.java",
        "**/DBCaseRepository.java",
        "**/MapperUtil.java",
        "**/CCDCaseApi.java",
        "**/ClaimIssueService.java",
        "launch-darkly-client/**",
        "email-client/**",
        "rpa-mapper/**",
        "**/fees/client/**",
        "**/UserRoleConstraintValidator.java",
        "**/ValidEmploymentConstraintValidator.java",
        "**/ValidInterestConstraintValidator.java",
        "**/ValidInterestDateConstraintValidator.java",
        "**/ValidUnemploymentConstraintValidator.java",
        "**/ValidIncomeConstraintValidator.java",
        "**/ValidExpenseConstraintValidator.java",
        "**/ValidResidenceConstraintValidator.java",
        "**/PaperResponseFullDefenceCallbackHandler.java",
        "**/OCON9xResponseUtil.java",
        "**/ResponseUtils.java",
        "**/cmc/claimstore/services/ccd/callbacks/roboticssupport/RoboticsNotificationServiceImpl.java"
    ].join(",")
  }
}

bootJar {
  archiveFileName.set('claim-store.jar')
  manifest {
    attributes 'Implementation-Title': project.name,
      'Implementation-Version': project.version
  }
}

task printVersion {
  description 'Prints the version of this project'
  doLast {
    print project.version
  }
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}

bootRun {
  def debug = System.getProperty("debug")
  if (debug == 'true') {
    jvmArgs = ['-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5005']
  }
}

task checkStyle(dependsOn: [checkstyleMain, checkstyleAat, checkstyleTest, checkstyleIntegrationTest]){}

idea {
  module {
    testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
    testSourceDirs += project.sourceSets.integrationTest.resources.srcDirs
    testSourceDirs += project.sourceSets.aat.java.srcDirs
    testSourceDirs += project.sourceSets.aat.resources.srcDirs
  }
}
